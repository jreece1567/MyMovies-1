buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

group = 'com.sandy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('com.h2database:h2')
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

task delombokHelp(type: DelombokTask) {
    args "--help"
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir // sourceSets.main.allJava
    // classpath = sourceSets.main.compileClasspath // configurations.compile
    options.overview = "src/main/resources/doc/overview.html"
    options.links "https://docs.oracle.com/javaee/7/api/"
    options.links "https://docs.oracle.com/javase/8/docs/api/"
    options.links "https://docs.spring.io/spring-boot/docs/2.0.4.RELEASE/api/"
    options.links "https://docs.spring.io/spring/docs/5.0.8.RELEASE/javadoc-api/"
    options.links "https://docs.spring.io/spring-boot/docs/current/api/"
    options.links "https://docs.spring.io/spring/docs/current/javadoc-api"
    options.links "https://docs.spring.io/spring-data/commons/docs/current/api/"
    options.links "https://docs.spring.io/spring-data/jpa/docs/current/api/"
    options.addStringOption('Xdoclint:none', '-quiet')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

checkstyle {
    toolVersion '8.11'
    configFile file("config/checkstyle/google_checkstyle.xml")
    maxWarnings 0
    maxErrors 0
}

pmd {
  toolVersion '5.8.1'
  ignoreFailures = true
  ruleSets = [
    'java-basic',
    'java-braces',
    'java-clone',
    'java-codesize',
    'java-design',
    'java-empty',
    'java-finalizers',
    'java-imports',
    'java-j2ee',
    'java-javabeans',
    'java-junit',
    'java-migrating',
    'java-naming',
    'java-optimizations',
    'java-strictexception',
    'java-strings',
    'java-sunsecure',
    'java-typeresolution',
    'java-unnecessary',
    'java-unusedcode'           
    ]
}

findbugs {
    toolVersion '3.0.1'
    excludeFilter file("config/findbugs/excludeFilter.xml")
    ignoreFailures true
    effort 'max'
    reportLevel 'low'
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

